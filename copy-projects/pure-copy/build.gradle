apply plugin: 'base'

ext {
    answer = 'answer'
}

task copyJavaImage(type: Copy) {
    from file('../images/java.jpg')
    into "${buildDir}/java-image"
}

// 演習2解答例(fromに複数のファイルを指定する)
task copyMultiImages(type: Copy, group: answer) {
    description 'exercise2'
    from '../images/java.jpg', '../images/jvm/groovy.png', '../images/jvm/scala.png'
    into "${buildDir}/jvm-langs"
}

task copyMultitimeFrom(type: Copy, group: answer) {
    description 'exercise2'
    ['java.jpg', 'jvm/groovy.png', 'jvm/scala.png'].each {
        from "../images/${it}"
    }
    into "${buildDir}/multi-images"
}

task copyDir(type: Copy) {
    from '../images/server'
    into "${buildDir}/copy-dir"
}

task copyTree(type: Copy) {
    from fileTree('../images/server')
    into "${buildDir}/copy-tree"
}

// 演習3解答例
task copyImages(type: Copy, group: answer) {
    description 'exercise3'
    from fileTree('../images')
    into "${buildDir}/images"
}

task copyOnlyPng(type: Copy) {
    from '../images/tool'
    into "${buildDir}/only-png"
    include '*.png'
}

task copyNotPng(type: Copy) {
    from '../images/tool'
    into "${buildDir}/not-png"
    exclude '*.png'
}

task copyComplex(type: Copy) {
    from ('../images/jvm') {
        exclude 'g*'
    }
    from ('../images/tool') {
        include 'g*'
    }
    into "${buildDir}/copy-complex"
}

// 演習6解答例
task copyMoreComplex(type: Copy, group: answer) {
    description 'exercise6'
    from (fileTree('../images')) {
        include '**/*.png'
        exclude '**/*oo*.*'
        include '**/j*.*'
    }
    into "${buildDir}/complex"
}
