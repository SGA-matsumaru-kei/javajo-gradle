apply plugin: 'base'

// 1. 何もしないタスク

task my1stTask

// 演習3解答例
my1stTask.group = 'my task'

// 2. 設定は施しているけど、何もしない my2ndTask

task my2ndTask {
    println "doing configuration for task[${name}]"
    group = 'my task'
    description 'do nothing'
}

// 3. 何らかの処理を行うタスク

task my3rdTask << {
    println "This is ${name}."
}

// 演習3解答例
my3rdTask.group 'my task'

// 4. あるタイプのタスク

class MyTask extends DefaultTask {
    String message = 'running'
    @TaskAction
    void doSomething() {
        logger.lifecycle "{} {}, on {}", message, name, new Date().format('yyyy-MM-dd HH:mm:ss')
    }
}

task my4thTask(type: MyTask, /*演習3解答例*/group: 'my task')

// 5. あるタイプのタスクに設定も施す

task my5thTask(type: MyTask) {
    group = 'my task'
    description = 'doing something'
    message = 'hello'
}

ext {
    pattern = 'yyyy-MM-dd HH:mm:ss.SSS'
}

// 演習2解答例
task timeOfConf {
    println(new Date().format(pattern))
}

// 演習2解答例
task timeOfExec {
    doLast {
        println (new Date().format(pattern))
    }
}
