apply plugin: 'base'

task langs {
    def outputFile = file('languages.txt')
    /* outputs.file で定義する */
    outputs.file outputFile
    doLast {
        def contents = [
                'Java', 'Groovy', 'Scala',
                'Clojure', 'JRuby'
        ].join('\n')
        // ファイルへの書き出しは必要です
        outputFile.write(contents, 'UTF-8')
    }
}

task osAndLang {
    def matrix = ['windows', 'linux', 'osx'].collect {os ->
        ['Java', 'Groovy', 'Scala'].collect {lang ->
            [os: os, lang: lang]
        }
    }.flatten()
    def outputFiles = matrix.collect {
        file("conbination/${it.os}${it.lang}.txt")
    }
    /* outputs.files で定義する */
    outputs.files outputFiles
    doLast {
        if (!file('conbination').exists()) {
            file('conbination').mkdirs()
        }
        matrix.each {
            file("conbination/${it.os}${it.lang}.txt")
                    .write("${it.os} - ${it.lang}", 'UTF-8')
        }
    }
}

// 演習2-1解答例
task fiveLangs(group: 'answer') {
    def fileName = '5-langs.txt'
    outputs.file fileName
    doLast {
        def contents = [Scala:3, Java:1, Groovy:2, Haskell:4, C:5].sort {
            it.value
        }.collect {
            it.key
        }.join('\n')
        file(fileName).write(contents, 'UTF-8')
    }
}

// 演習2-2解答例
task fiveLangsToMd(group: 'answer') {
    def inName = '5-langs.txt'
    def outName = '5-langs.md'
    inputs.file inName
    outputs.file outName
    doLast {
        file(inName).eachLine('UTF-8') {
            file(outName) << "* ${it}"
            file(outName) << '\n'
        }
    }
}

task showLanguages {
    inputs.file 'language-list.txt'
    doLast {
        println file('language-list.txt').text
    }
}

task langTypes {
    def dir = "${projectDir}/"
    inputs.files fileTree('langs')
    doLast {
        fileTree('langs').collect {
            "${it}".replace(dir, '')
        }.each {
            println it
        }
    }
}

task langTypesByDir {
    def dir = "${projectDir}/"
    inputs.dir file('langs')
    doLast {
        fileTree('langs').collect {
            "${it}".replace(dir, '')
        }.each {
            println it
        }
    }
}

task showLanguagesAsSource {
    inputs.source 'language-list.txt'
    doLast {
        println file('language-list.txt').text
    }
}

task langTypesBySourceDir {
    def dir = "${projectDir}/"
    inputs.sourceDir file('langs')
    doLast {
        fileTree('langs').collect {
            "${it}".replace(dir, '')
        }.each {
            println it
        }
    }
}

task langsWithUpToDate {
    def outputFile = file('languages.txt')
    def contents = [
            'Clojure', 'Groovy', 'Scala', 'Java', 'JRuby'
    ].join('\n')
    outputs.file outputFile
    outputs.upToDateWhen {
        outputFile.exists() && outputFile.text == contents
    }
    doLast {
        outputFile.write(contents, 'UTF-8')
    }
}

// 演習5解答例
task randomJvmLang(group: 'answer') {
    def list = ['Java', 'Groovy', 'Scala']
    def lang = list[new Random().nextInt(list.size())]
    def fileName = 'jvm-language.txt'

    outputs.file fileName
    outputs.upToDateWhen {
        outputs.files.singleFile.exists() && outputs.files.singleFile.text == lang
    }
    doLast {
        logger.lifecycle "file contents[$lang]"
        if (outputs.files.singleFile.exists()) {
            logger.lifecycle "previous [${outputs.files.singleFile.text}]"
        }
        outputs.files.singleFile.write(lang, 'UTF-8')
    }
}
