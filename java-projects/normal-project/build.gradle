apply plugin: 'java'
apply plugin: 'jacoco'

ext {
    javaVersion = 1.8
    encoding = 'UTF-8'
}

defaultTasks('clean', 'build')

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3', ext: 'jar'
    compile 'org.jetbrains:annotations:13.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core-java8:1.0.0m1'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    options.encoding = encoding
}

test {
    jacoco {
        append = false
        destinationFile = file("${buildDir}/jacoco/jacocoTest.exec")
        classDumpFile = file("${buildDir}/jacoco/classpathdumps")
    }
    exclude '**/*Test$*'
    ignoreFailures = true
}

task showJacocoReportUrl(dependsOn: 'jacocoTestReport') {
    onlyIf {
        tasks.jacocoTestReport.didWork
    }
    doLast {
        logger.lifecycle "Jacoco reports are available at file://${tasks.jacocoTestReport.reports.html.entryPoint}"
    }
}

task covorage(dependsOn: ['clean', 'test']) {
    finalizedBy tasks.showJacocoReportUrl
    group = 'verification'
    description = 'Runs Jacoco covorage tool.'
}
