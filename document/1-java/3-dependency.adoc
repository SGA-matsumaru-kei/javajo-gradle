= 依存性管理

依存性管理というのはどのような言語にも必ず存在する問題です。

依存するライブラリーのバージョンが異なるためにビルドできない、実行できないといった問題として発生します。

また、かつてのJavaのプロジェクトではプロジェクトのディレクトリー内に `lib` と名のついたディレクトリーを準備して、そこに依存するライブラリーを配置して利用する方法などがとられてきました。
この方法による問題は次のとおりです。

* ソフトウェアが大きくなるに伴い、ライブラリーのバージョンを上げるためのコストが肥大化する
* プロジェクトをビルドできる環境の構築にかかるコストが増える
** どれそれでのマシンでしかビルドできない

依存性管理ではプロジェクトが依存するライブラリーのバージョンの管理・分析、バージョンのコンフリクト(ライブラリーが同一でもバージョンが異なること)の検出などの機能を提供します。

== 依存性管理の単位

Gradleでは依存性管理の単位として `Configuration` を提供しています。 `Configuration` はファイルの集合です。 `Configuration` に依存ライブラリーを宣言することで、プロジェクトの依存ライブラリーを管理していきます。

この `Configuration` を管理するのが `ConfigurationContainer` で、 DSLでは `configurations` オブジェクトでアクセスすることができます。

== Javaプロジェクトにおける `Configuration`

Javaプロジェクトでは次の `Configuration` がデフォルトで提供されています。

.Javaプロジェクトのデフォルト `Configuration`
|===
|名前 |継承元 |利用シーン

|`compile`
|-
|Javaソースコードをコンパイルする際に参照する依存ライブラリー

|`runtime`
|`compile`
|作成されたJavaアプリケーション(ライブラリー)を動かす際に参照する依存ライブラリー

|`testCompile`
|`compile`
|テスト用のJavaソースコードをコンパイルする際に参照する依存ライブラリー

|`testRuntime`
|`testCompile` +
`runtime`
|テストを動かす際に参照する依存ライブラリー

|`default`
|`runtime`
|このプロジェクトの成果物を利用する際に依存ライブラリー

|`archives`
|-
|成果物をアップロードする際に参照する依存ライブラリー(`maven` プラグインで利用する)
|===

.依存ライブラリーの指定(再掲)
[TIP]
====

依存ライブラリーを指定する場合は `dependencies{}` ブロック内で次のように指定します。

.依存ライブラリーの指定
[source,groovy]
----
dependencies {
  /* Stringで指定する場合 */
  configurationName 'groupName:artifactId:version'
  /* Mapで指定する場合 */
  configurationName group: groupName, name: artifactId, version: version
}
----

====

=== 演習1

* `java-projects/dep-project/java-conf` ディレクトリーに移動してください。
* `dependencies` タスクによって、上述のような `Configuration` の継承関係があることを確認してください。

なお、このプロジェクトには次のような依存ライブラリーを指定しています。

.依存ライブラリー
|===
|`Configuration` |`group` |`name` |`version`

|`compile`
|`javax.json`
|`javax.json-api`
|`1.0`

|`runtime`
|`com.owlike`
|`genson`
|`1.3`

|`testCompile`
|`javax.inject`
|`javax.inject-tck`
|`1`

|`testCompile`
|`junit`
|`junit`
|`4.12`

|`testRuntime`
|`com.squareup.dagger`
|`dagger`
|`1.2.2`
|===

== 依存性の指定いろいろ

ここまでは、依存ライブラリーの指定方法は `group` 、 `name`(`artifactId`)、 `version` を指定する方法だけを扱ってきました。
これは実際にはExternal module dependenciesと呼ばれる外部ライブラリーを参照する依存性の一つです。

.様々なdependencies
* External module dependencies
* Client module dependencies
* Project dependencies
* File dependencies
* Gradle API dependencies

ここではProject dependenciesおよびFile dependenciesについて説明していきます。

.他のdependencies
[NOTE]
====

今回は省略したClient module dependenciesおよびGradle API dependenciesは次のようなものです。

.Client module dependencies
NOTE: より細かくtransitive dependencies(推移的依存性)を指定したい場合に用います。

.Gradle API dependencies
NOTE: Gradleプラグインを作成する場合に用います。

====

=== Project dependencies

複数のプロジェクト構成のプロジェクトで、あるサブプロジェクトが他のサブプロジェクトを依存ライブラリーとして指定する

.Project dependenciesの指定方法
[source,groovy]
----
dependencies {
  compile project(':sub-project-name')
}
----

サブプロジェクト(`sub-project-name`)にてExternal module dependenciesが指定されている場合、このサブプロジェクトを参照しているプロジェクトはサブプロジェクト(`sub-project-name`)の `default` `Configuration` に依存するようになります。

.`project` メソッド
[TIP]
====

`project(String)` メソッドはプロジェクトのサブプロジェクトを取得するメソッドです。指定するプロジェクトの文字列はプロジェクト名の頭にセミコロン(`:`)を付与したプロジェクトパス形式の文字列を指定します。

.ディレクトリー構成
[source]
----
root
├── child1
│   └── grandchild
└── child2
----

例えば、上記のように `child1` と `child2` というサブプロジェクト、 `child1` の下に `grandchild` という孫プロジェクトで構成されるプロジェクトを想定します。

このとき、 `root` プロジェクトから見て、それぞれのパスは次のようになります。

.プロジェクトのパス
|===
|プロジェクト名 |プロジェクトパス

|`child1`
|`:child1`

|`child2`
|`:child2`

|`grandchild`
|`:child1:grandchild`
|===

この時に、 `root` プロジェクトが `child2` プロジェクトを参照する場合は `project(':child2')` と `project` メソッドを呼び出します。

====

=== File dependencies

直接jarファイルを依存ライブラリーとして指定します。なお、指定する場合はファイルの数が一つであっても `files` メソッドに渡してください。

.File dependenciesの指定方法
[source,groovy]
----
dependencies {
  /* filesメソッドでFileCollectionを渡します */
  compile files('libs/junit.jar')
}
----

==== 演習2

`java-projects/dep-project` に移動してください。

.下記の条件を満たす依存関係を `dep-project` に指定してください
* `compile` が サブプロジェクト `java-conf` に依存する
** ただし、 `java-conf` プロジェクトへの正しいパス文字列(`:java-conf`)を取得するために、 `getSubProject` というメソッドを作成してありますので、それを利用してください
* `testCompile` がjarファイル `libs/junit.jar` に依存する

なお、確認は `dependencies` タスクでも構いません。
また、 `showTestDep` という `testRuntime` に登録されているファイルをすべて表示するタスクを作ってありますので、そちらも使ってください。

次のように出力されれば成功です。

.`dependencies` の場合
[source]
----
compile - Compile classpath for source set 'main'.
\--- project :java-conf
     +--- javax.json:javax.json-api:1.0
     \--- com.owlike:genson:1.3

default - Configuration for default artifacts.
\--- project :java-conf
     +--- javax.json:javax.json-api:1.0
     \--- com.owlike:genson:1.3

runtime - Runtime classpath for source set 'main'.
\--- project :java-conf
     +--- javax.json:javax.json-api:1.0
     \--- com.owlike:genson:1.3

testCompile - Compile classpath for source set 'test'.
\--- project :java-conf
     +--- javax.json:javax.json-api:1.0
     \--- com.owlike:genson:1.3

testRuntime - Runtime classpath for source set 'test'.
\--- project :java-conf
     +--- javax.json:javax.json-api:1.0
     \--- com.owlike:genson:1.3
----

.`showTestDep` の場合
[source]
----
:showTestDep
/path/to/javajo-gradle/java-projects/dep-project/libs/junit.jar
/path/to/javajo-gradle/java-projects/dep-project/java-conf/build/libs/java-conf.jar
/path/to/home/.gradle/caches/.../javax.json-api-1.0.jar
/path/to/home/.gradle/caches/.../genson-1.3.jar
----

== Transitive dependencies(推移的依存性) Management

Gradleの依存性管理はtransitive dependencies(推移的依存性)を含めた依存性の管理機能を提供しています。

=== Transitive dependencies

プロダクションの依存ライブラリーが他の複数のライブラリーに依存している時に、このプロダクションはそれらすべての依存ライブラリーに依存することになります。
このような連鎖的な依存性をTransitive dependenciesと呼びます。
Transitive dependenciesはTree構造のデータとして表現されます。
このTransitive dependenciesを適切に扱わない場合、ビルドは容易に壊れてしまいます。
特に依存するライブラリーは同じでもバージョンが異なるものがTreeの中にある場合はその傾向が顕著になります。
Gradleを含むビルドツールはたいていバージョンのコンフリクトを解決する手段を持っていますが、Gradleはその解決を柔軟に提供する手段を提供しています。

.Transitive dependencies
====

.Guice4.0というDIライブラリー
[source]
----
com.google.inject:guice:4.0
├── javax.inject:javax.inject:1
├── aopalliance:aopalliance:1.0
└── com.google.guava:guava:16.0.1
----

.airlift-stats(airliftというRESTサーバーを簡単に作るライブラリーに組み込まれているパフォーマンス計測ライブラリー)
[source]
----
io.airlift:stats:0.116
├── io.airlift:units:0.116
│   ├── javax.validation:validation-api:1.1.0.Final
│   └── com.google.guava:guava:18.0
├── org.weakref:jmxutils:1.18
├── com.google.guava:guava:18.0
├── com.fasterxml.jackson.core:jackson-annotations:2.4.4
├── com.google.code.findbugs:annotations:2.0.3
├── io.airlift:slice:0.10
└── org.openjdk.jol:jol-core:0.1
----

上記のGuice4.0とairlift-stats0.116を依存ライブラリーとして指定した場合、
guavaのバージョンがコンフリクトします。

Gradleではこれらをどのように解決するでしょうか？

.`build.gradle`
[source,groovy]
----
dependencies {
  compile 'io.airlift:stats:0.116'
  compile 'com.google.inject:guice:4.0'
}
----

.`dependencies` タスクの実行結果
[source]
----
+--- io.airlift:stats:0.116
|    +--- io.airlift:units:0.116
|    |    +--- javax.validation:validation-api:1.1.0.Final
|    |    \--- com.google.guava:guava:18.0
|    +--- org.weakref:jmxutils:1.18
|    +--- com.google.guava:guava:18.0
|    +--- com.fasterxml.jackson.core:jackson-annotations:2.4.4
|    +--- com.google.code.findbugs:annotations:2.0.3
|    +--- io.airlift:slice:0.10
|    \--- org.openjdk.jol:jol-core:0.1
\--- com.google.inject:guice:4.0
     +--- javax.inject:javax.inject:1
     +--- aopalliance:aopalliance:1.0
     \--- com.google.guava:guava:16.0.1 -> 18.0
----

最後の行に注目してください。

[source]
----
\--- com.google.guava:guava:16.0.1 -> 18.0
----

guavaのバージョン16.0.1の横に、 `-> 18.0` とついています。これはguice4.0のguavaへの依存は18.0を使うことを表します。
Gradleはデフォルトでバージョンの大きい方に合わせるようにコンフリクトを解決します。

====

==== 演習3

`java-projects/dep-project/java-conf` に移動して、 `dependencies` タスクを実行して、実際にバージョンコンフリクトがどのように解決されているか確認してください。

=== Transitive dependenciesのハンドリング

Transitive dependenciesに対する柔軟な操作の一部を紹介します。

.Transitive dependenciesを無視する
====

Transitive dependenciesを解決しないという選択を行えます。

.Transitive dependenciesの解決を無効にする
[source, groovy]
----
configurations.myConfiguration {
  transitive = false
}
----

====

==== 演習4

「Transitive dependenciesを無視する」方法を参考にして、 `java-projects/dep-project/java-conf` の `testCompile` `Configuration` に対しては transitive dependencies を解決しないように設定してください。
なお、確認方法は `dependencies` タスクで行ってください。

'''

.特定のモジュールを除外する
====

ある依存ライブラリーのtransitive dependenciesを除外することができます。

.特定のモジュールを除外する方法1
[source,groovy]
----
dependencies {
  testCompile('junit:junit:4.12') {
    exclude module: 'hamcrest-core'
  }
}
----

.特定のモジュールを除外する方法2
[source,groovy]
----
configurations {
  testCompile {
    exclude module: 'hamcrest-core'
  }
}
----

====

==== 演習5

「特定のモジュールを除外する」方法を参考にして、それぞれを試してください。
なお、確認方法は `dependencies` タスクで行ってください。

'''

.バージョンコンフリクトが発生した場合はBUILD FAILEDにする
====

.バージョンのコンフリクトが発生する場合にビルドを失敗させる方法
[source,groovy]
----
/* すべてのconfigurationでバージョンのコンフリクトが発生したら落ちる */
configurations.all {
  resolutionStrategy {
    failOnVersionConflict()
  }
}
----

====

==== 演習6

「バージョンコンフリクトが発生した場合はBUILD FAILEDにする」方法を参考にして、すべての `Configuration` でバージョンコンフリクトが発生した場合に落ちるように設定してください。
なお、確認は `dependencies` タスクで行ってください。

'''

.特定のライブラリーのバージョンを固定する
====

Transitive dependenciesを含めて、特定のライブラリーのバージョンを固定にします。

.特定のライブラリーのバージョンを固定化する方法
[source,groovy]
----
configurations.all {
  resolutionStrategy {
    force 'junit:junit:4.12'
  }
}
----

====

==== 演習7

「特定のライブラリーのバージョンを固定する」方法を参考にして、すべての `Configuration` で `junit` のバージョンを `4.12` になるように設定してください。
なお、確認は `dependencies` タスクで行ってください。

= まとめ

* Gradleにおける依存性管理の方法について簡単に学習しました。
** プロダクションで利用するライブラリー管理に役立たせてください。
* 依存性解決のためにはレポジトリーに問い合わせなければなりませんが、そのレポジトリーについては特にふれませんでした。
** このセクションの付録に簡単に概要をつけていますので、そちらをご覧ください。
* 作成したライブラリーをレポジトリーに登録する方法についてふれませんでした。
** https://docs.gradle.org/current/userguide/publishing_maven.html[公式のドキュメント] を参考にしてください。

.レポジトリー
[TIP]
======

`dependencies{}` ブロックで宣言された依存性は登録されたレポジトリーに問い合わせて、解決・取得します。Gradleで利用できるレポジトリーには以下のものが挙げられます。

.レポジトリーの種類
|===
|レポジトリーの種類 |詳細

|Maven Central repository
|Gradleが標準で提供するMaven Central repositoryへの参照

|Maven JCenter repository
|Gradleが標準で提供するBintray JCenter repositoryへの参照 +
最近はMaven Centralよりももjcenterレポジトリーを利用するほうが多い(理由は後述)

|Maven local repository
|Gradleが標準で提供するlocal Maven repository(`/path/to/user/.m2/repository`)への参照

|Maven repository
|Mavenタイプのレポジトリー。url、ユーザー情報などは設定する必要がある

|Ivy repository
|Ivyタイプのレポジトリー。url、ユーザー情報などは設定する必要がある

|Flat directory repository
|マシン上のファイルシステムに構築されたレポジトリー
|===


.Maven Central repository
[NOTE]
====

Maven Central (http://repo1.maven.org/maven2) に問い合わせるための設定です。

.Maven Centralを利用するレポジトリーの設定
[source,groovy]
----
repositories {
  mavenCentral()
}
----

なお、Maven CentralはHTTPのみサポートされており、HTTPSはサポートされていません。
HTTPSを利用したい場合はJCenterを利用したほうがよいでしょう。

====

.Maven JCenter repository
[NOTE]
====

BintrayのJCenterレポジトリーはほぼすべてのポピュラーなOSSを集めたレポジトリーです(Maven Centralのキャッシュも含む)。また、Bintrayに直接上げられたものも含みます。

Maven JCenter レポジトリー (https://jcenter.bintray.com) に問い合わせるための設定です。

.Maven JCenterを利用するレポジトリーの設定
[source,groovy]
----
repositories {
  jcenter()
}
----

なお、JCenterをHTTPSではなく、HTTPでアクセスしたい場合は次のように設定してください。

.Maven JCenterをHTTPで利用する
[source,groovy]
----
repositories {
  jcenter {
    url 'http://jcenter.bintray.com/'
  }
}
----

====

.Maven Local repository
[NOTE]
====

Maven local repositoryに問い合わせるための設定です。

.Maven local repositoryを利用するレポジトリーの設定
[source,groovy]
----
repositories {
  mavenLocal()
}
----

通常は `/path/to/home/.m2/repository` を参照しますが、 `/path/to/home/.m2/settings.xml` あるいは `M2_HOME/conf/settings.xml` でレポジトリーの場所が変更されている場合は、そちらを参照します。

====

.Maven repository
[NOTE]
====

独自のMaven repositoryに問い合わせるための設定です。

.独自Maven repositoryの設定
[source,groovy]
----
repositories {
  maven {
    url 'https://repo.your-domain.com/m2'
    credentials {
      username 'user'
      password 'password'
    }
    authentication {
      /* digest認証の場合 */
      digest(DigestAuthentication)
    }
  }
  maven {
    url 'http://repo-another.com/m2'
    credentials {
      username 'user-name'
      password 'pass-word'
    }
    authentication {
      /* basic認証の場合 */
      basic(BasicAuthentication)
    }
  }
  maven {
    /* s3を使う場合 */
    url 's3://my-bucket/m2'
    credentials(AwsCredentials) {
      accessKey 'access'
      secretKey 'secret'
    }
  }
}
----

.認証情報の保存場所
[TIP]
=====

認証情報を `build.gradle` に直接記述するのはセキュリティ的に危険です。この場合は下記のファイルにプロパティとして設定しておくのがよいでしょう。

.`/path/to/home/.gradle/gradle.properties`
[source]
----
s3AccessKey=xxxxxxxx
s3SecretKey=XXXXXXXXX
----

なお、この値は `properties` タスクを実行した時に表示されてしまいますので、人前では `properties` タスクを **絶対に実行しない** でください。

=====

====

.Ivy repository
[NOTE]
====

独自のIvy repositoryに問い合わせるための設定です。

.Ivy repositoryの設定
[source,groovy]
----
repositories {
  ivy {
    url 'http://repo.your-domain.com/ivy'
    layout 'maven'
    /* basic認証が必要な場合 */
    credentials {
      username 'user-name'
      password 'pass-word'
    }
  }
}
----

`layout` はレポジトリーのディレクトリーのレイアウトで、次の値が指定できます。

.`layout` の値
|===
|値 |artifactのURI |ivy.xmlのURI

|`gradle`
|`$baseUri/[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier])(.[ext])`
|`$baseUri/[organisation]/[module]/[revision]/ivy-[revision].xml`

|`maven`
|`$baseUri/[orgPath]/[module]/[revision]/[artifact]-[revision](-[classifier])(.[ext])`
|`$baseUri/[orgPath]/[module]/[revision]/ivy-[revision].xml`

|`ivy`
|`$baseUri/[organisation]/[module]/[revision]/[type]/[artifact](.[ext])`
|`$baseUri/[organisation]/[module]/[revision]/[type]/[artifact](.[ext])`

|`pattern`
|第二引数に渡す `Closure` の中で `artifact` メソッドにパターンを指定する
|第二引数に渡す `Closure` の中で `ivy` メソッドにパターンを指定する
|===

====

.File directory repository
[NOTE]
====

マシン内のファイルシステムに問い合わせるための設定です。

.File directory repositoryの設定
[source,groovy]
----
repositories {
  flatDir {
    dirs 'libs'
  }
}
----

File directory repositoryを使う場合はメタデータ(ivy.xmlやpom.xml)を利用できないため、メタデータを自動生成します。ただし、他のレポジトリーからメタデータを利用できる場合はそちらを優先して利用します。

また本セクションでは `libs/junit.jar` というファイルを使用しましたが、グループ名がこの場合は不明だったため、次のような依存性の定義をおこなうこともできます。

.groupおよびversionが特定できないためnameで指定することが可能
[source,groovy]
----
repositories {
  flatDir {
    dirs 'libs'
  }
}
dependencies {
  testCompile name: 'junit'
}
----

====

======
