= 簡単なJavaのサンプルプロジェクト

`java-projects/simple-project` に移動してください。

このディレクトリーは先ほど `init` タスクで `--type java-library` で作成したような構成になっており、
`build.gradle` ファイルは現在ほぼ空っぽになっています。

ここにJavaアプリケーション用のビルドスクリプトを書いていきます。

最終的にプロジェクトの `build` タスクが実行できるようになることが目標です。

== `java` プラグインを適用する

Gradleは単なるタスクの自動実行基盤なので、プラグインを何も適用していない場合は、ほとんど何もできません。

今のこのプロジェクトもプラグインを何も適用していないので、ほぼ何もできません。

=== 演習0

`java-projects/simple-project` がほぼ何もできないことを確かめてみてください。
Gradleでは `tasks` タスクによって、現在できること(実行できるタスク)を確認することができます。

.Gradleが何をすることができるか確かめる方法
[source,sh]
----
$ gradle tasks
----

Javaに関するタスクが何も表示されないことを確認してください。

=== 演習1

Javaに関するタスクが利用可能になるように、 `java` プラグインを適用してください。

.Gradleにプラグインを適用する方法(その1)
[source,groovy]
----
apply plugin: 'plugin-id'
----

.Gradleにプラグインを適用する方法(その2)
[source,groovy]
----
plugins {
  id 'plugin-id' version 'plugin-version'
}
----

.`tasks` タスクによって、次のように表示されれば `java` プラグインが適用できた状態です。
[source,text]
----
------------------------------------------------------------
All tasks runnable from root project
------------------------------------------------------------

Build tasks
-----------
assemble - Assembles the outputs of this project.
build - Assembles and tests this project.
buildDependents - Assembles and tests this project and all projects that depend on it.
buildNeeded - Assembles and tests this project and all projects it depends on.
classes - Assembles classes 'main'.
clean - Deletes the build directory.
jar - Assembles a jar archive containing the main classes.
testClasses - Assembles classes 'test'.

Documentation tasks
-------------------
javadoc - Generates Javadoc API documentation for the main source code.

Verification tasks
------------------
check - Runs all checks.
test - Runs the unit tests.


----

また、まだこの段階ではコンパイルができないことを `classes` タスクを実行して確認してください。

[source,text]
----
$ gradle classes
----

=== 演習2

プロジェクト内で使うことのできる変数を設定してください。

.設定して欲しい変数
|===
|変数名 |型 |値

|`jdk`
|`String` or `double`
|`1.8`

|`encoding`
|`String`
|`UTF-8`
|===

.Gradleでの変数
[TIP]
====

Gradleでの変数はプロパティとも呼ばれます。
これは `project` オブジェクトの `properties` に値を保持するためです。
具体的にどのような形でプロパティが提供されるのかは後述しますが、
ここではユーザーによるextraプロパティと呼ばれるプロパティの定義方法を紹介します。

.extraプロパティでのプロパティ定義
[source,groovy]
----
ext.myName = '@mike_neck'
ext {
  junitVersion = '4.12'
  junit = "junit:junit:${junitVersion}"
}
----

extraプロパティに値を定義することによって、ビルドスクリプト内部ではプロパティ名としてその値を利用することが可能になります。

.extraプロパティの利用
[source,groovy]
----
println myName /* -> @mike_neck と表示される */
----

====

.Groovyの `String` について
[TIP]
====

Groovyの `String` リテラルはシングルクォート( `'` )で囲むことで記述することができます。
`char` 型はどうなるのか心配されるかもしれませんが、Groovyの `String` には `as(Class<?>)` というメソッドによって
`char` 型に変換することが可能です。

.Groovyの `String` リテラル
[source,groovy]
----
def myName = '@mike_neck'
def a = 'a' as char /* -> char の a */
def x = 'xyz' as char /* -> char の x */
def one = '1' as int /* -> int の 1 */
def ten = '10-java' as int /* -> NumberFormatException */
----

さらにGroovyには `GString` という `String` を拡張したクラスがあります。
そちらのリテラルはダブルクォート( `"` )で囲みます。
`GString` では `${}` の中で式を展開して文字列に埋め込むことができます。

.Groovyの `GString`
[source,groovy]
----
def myName = "@mike_neck"
def hello = "Hello, $myName." /* -> Hello, @mike_neck. */
def calc = "1 + 1 = ${1 + 1}" /* -> 1 + 1 = 2 */
def midNight = "Good night, ${myName.replace('_', '')}." /* -> Good night, @mikeneck. */
----

====

=== 演習3

コンパイルが落ちないようにするために下記のライブラリーをコンパイル時に参照できるようにしてください。

.ライブラリー
|===
|属性 |値

|`group`
|`org.jetbrains`

|`name`(`artifactId`)
|`annotations`

|`version`
|`13:0`
|===


.ライブラリーを参照できるようにする方法
[source,groovy]
----
dependencies {
  /* ライブラリー参照の方法1 */
  compile group: 'group.name', name: 'library-name', version: 'library-version'
  /* ライブラリー参照の方法2 */
  compile 'group.name:library-name:library-version'
}
----

ライブラリーの指定方法は下記のいずれかで指定できます。

.`Map` で指定する方法
|===
|キー |指定する値

|`group`
|Mavenの `groupId`

|`name`
|Mavenの `artifactId`

|`version`
|Mavenの `version`
|===

.`String` で指定する方法
[source,groovy]
----
/* groupIdとartifactIdとversionをコロン(:)でつなぐ */
'groupId:artifactId:version'
----

=== 演習4

まだ依存性(ライブラリー)を解決できないので、依存性を解決するためのレポジトリーにjcenterリポジトリーかmaven centralリポジトリーを登録してください。

.依存性解決用のレポジトリーを登録する方法
[source,groovy]
----
repositories {
  /* Maven Central レポジトリーを依存性解決に使う */
  mavenCentral()
  /* jcenterレポジトリーを依存性解決に使う */
  jcenter()
  /* 独自のmavenレポジトリーを依存性解決に使う */
  maven {
    /* レポジトリーのURL */
    url 'https://repo.my-company.com/m2'
    /* レポジトリーに認証が必要な場合 */
    credentials(PasswordCredentials) {
      username myRepositoryUser
      password myRepositoryPassword
    }
  }
  /* 独自のivyレポジトリーを依存性解決に使う */
  ivy {
    url 'https://repo.my-company.com/ivy'
    credentials {
      username = ivyRepoUser
      password = ivyRepoPassword
    }
  }
}
----

.`credentials` が `PasswordCredentials` の場合に設定する項目
* `username` - ユーザー名
* `password` - パスワード

.`credentials` が `AwsCredentials` の場合に設定する項目
* `accessKey` - アクセスキー
* `secretKey` - シークレットキー

以上の状態で `dependencies` タスクを実行すると次のように表示されるか確認してください。

.`dependencies` タスクの実行結果
[source,text]
----
:dependencies

------------------------------------------------------------
Root project
------------------------------------------------------------

archives - Configuration for archive artifacts.
No dependencies

compile - Compile classpath for source set 'main'.
\--- org.jetbrains:annotations:13.0

default - Configuration for default artifacts.
\--- org.jetbrains:annotations:13.0

runtime - Runtime classpath for source set 'main'.
\--- org.jetbrains:annotations:13.0

testCompile - Compile classpath for source set 'test'.
\--- org.jetbrains:annotations:13.0

testRuntime - Runtime classpath for source set 'test'.
\--- org.jetbrains:annotations:13.0

BUILD SUCCESSFUL
----

=== 演習5

現在の状態でも `classes` タスクに失敗する環境の人もいますので、コンパイルオプションを下記の通り設定してください。

.Javaコンパイルオプションの設定
|===
|設定したい項目 |設定したい値

|`-source` オプション
|`1.8`

|`-target` オプション
|`1.8`

|`-encoding` オプション
|`UTF-8`
|===

要件にもよりますが、リストを用いる方法あるいは、 `tasks.withType(Class<? extends Task>)` を用いる方法のいずれかで設定します。

.リストを用いる場合
[source,groovy]
----
sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
----

.`tasks.withType(Class<? extends Task>)` を用いる場合
[source,groovy]
----
tasks.withType(JavaCompile) {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
  options.encoding = 'UTF-8'
}
----

.Gradleのプロパティ
[TIP]
====

Gradleでは次の機序でプロパティを探します

.プロパティの5つのスコープ
. `project` オブジェクト自信のプロパティ
. extraプロパティ
. プラグインによって追加されたextension
. プラグインによって追加されたconventionプロパティ
. プロジェクトのタスク名
. 親プロジェクトで追加されたextraプロパティとconventionプロパティ

なお、

* 本勉強会ではプラグインを作ることは学習の対象外なので、2.と3.については特に説明しません。
** プラグインで追加されるプロパティがあるという記述にとどめます。
* サブプロジェクトについても学習の対象外としているので、6.についても詳しくは説明しません。

====

この段階で `classes` タスクが実行できるようになっているか確認してください。

=== 演習6

現在の状態でjarファイルを作ることができますが、テストを実行していないので、生成されるjarファイルの品質が疑わしい状態です。
そこでこの演習ではテストを実行できるようにします。

. タスク `testClasses` を実行するとBUILD FAILEDになることを確認して下さい。
. タスク `testClasses` は依存ライブラリーが足りないためにビルドが失敗しています。そこで、下記の必要な依存ライブラリーをテストコンパイルスコープに追加してください。
** `groupId` - `junit`
** `artifactId` - `junit`
** `version` - `4.12`
. 上記のライブラリーを追加した後、タスク `testClasses` が実行できることを確認してください。
. タスク `test` を実行して、このプロジェクトのJavaコードに対するテストをおこなってください。

.`java` プラグインにより追加される `configuration`
[TIP]
====

`java` プラグインにより追加される `configuration` には以下のものがあります。

.`java` プラグインにより追加される `configuration`
|===
|`configuration` 名 |内容

|`compile`
|プロダクションクラスのコンパイル時に依存するライブラリー

|`runtime`
|実行時に依存するライブラリー(コンパイルには用いられない)

|`testCompile`
|テストクラスのコンパイル時に依存するライブラリー

|`testRuntime`
|テスト実行時に依存するライブラリー
|===

====


