= Gradleのタスクの種類

Gradleのタスクには様々なタイプのタスクがあります。

* `JavaCompile`, `GroovyCompile`, 'Javadoc' などのコンパイル系タスク
* `Exec`, `JavaExec` などの実行系タスク
* `Test`, `FindBugs`, `CheckStyle`, `CodeNarc`, `Pmd` などのチェック系タスク
* `PublishToMavenRepository`, `PublishToIvyRepository`, `Upload` などのアップロード系タスク
* `Zip`, `Tar`, `War`, `Ear` などのアーカイブ系タスク

これら全てを取り扱うのは1日では厳しいので、本勉強会の最後の章としてアーカイブ系タスクを取り上げることにします。

== アーカイブタスクの基礎としてのコピータスク

アーカイブ系のタスクはどのような働きをするものか確認しましょう。

* 前提条件
** アーカイブ対象のファイル群もしくはファイルがはいったツリーがある
** 書き出すファイル名が決まっている
* 処理内容
** 対象のファイルを選定する
** ファイルを一箇所にコピーする
** 集めたファイルをアーカイブする

アーカイブ系のタスクはだいたいこのような動作をします。

ポイントとしてアーカイブ系のタスクは必ず次の共通点があります。

* 入力となるファイルがある
* 出力先の設定がある
* 入力となるファイルを一度 *コピー* する

アーカイブ系のタスクにはコピーをするという共通の動作があります。

したがって、アーカイブ系タスクの基礎は `Copy` タスクです。

実際、 `Zip`, `Jar`, `Tar`, `War`, `Ear` タスククラスは `AbstractCopyTask` を継承したタスクになっています。

.この章で扱うファイルについて
[NOTE]
====

この章で扱うファイルは `copy-projects/images` ディレクトリーにあります。このディレクトリーの構造は次のようになっています。

.ディレクトリー構造
[source,text]
----
copy-projects/images/
├── java.jpg
├── jvm
│   ├── groovy.png
│   ├── java.jpg
│   └── scala.png
├── server
│   ├── hadoop.jpg
│   ├── solr.png
│   └── web
│       ├── jetty.png
│       └── tomcat.png
└── tool
    ├── gradle.gif
    ├── maven.png
    └── sbt.jpg
----

====

= `Copy` タスクの作り方

`copy-projects/pure-copy` ディレクトリーに移動してください。

`Copy` タスクの作り方の参考例として、 `copy-projects/images` ディレクトリーにある `java.jpg` ファイルを `copy-projects/pure-copy/build/java-image` ディレクトリーにコピーするタスクを取り上げます。

== 単一ファイルのコピー

.`Copy` タスクの作り方
[source,groovy]
----
task copyJavaImage(type: Copy) { /* <1> */
  from file('../images/java.jpg') /* <2> */
  into file("${buildDir}/java-image") /* <3> */
}
----
<1> タスクの `type` に `Copy` を指定します。
<2> `from` メソッドで入力ファイルを指定します。
<3> `into` メソッドで出力先を指定します。

=== 演習1

.`copy-projects/pure-copy` ディレクトリーの `build.gradle` に上記のタスクが定義されていますので、実行してください。
* `build/java-image` ディレクトリーに `java.jpg` ファイルがコピーされているか確認して下さい。

== 複数ファイルのコピー

.複数のファイルをコピーするタスクの作り方は下記の二通りあります。
. `from` に複数のファイルを指定する
. `from` を複数回使う


.`from` に複数のファイルを指定する
[source,groovy]
----
task copyMultiImages(type: Copy) {
  from '../images/java.jpg', '../images/jvm/groovy.png', '../images/jvm/scala.png'
  into "${buildDir}/jvm-langs"
}
----

.`from` を複数回使う
[source,groovy]
----
task copyMultiFromImages(type: Copy) {
  ['java.jpg', 'jvm/groovy.png', 'jvm/scala.png'].each {
    from "../images/${it}"
  }
  into "${buildDir}/multi-images"
}
----

=== 演習2

.`../images/` 以下にある複数のファイルをコピーするタスクを作ってください。
* `from` に複数のファイルを指定するやり方
* `from` を複数回使うやり方

.`from`/`into` の引数は `String`? `File`?
[TIP]
====

`project` オブジェクトの `file` メソッドで評価されるので、 `String` 、 `File` いずれで指定してもかまいません。

.どちらの定義も同じ動作をする
[source,groovy]
----
task copyByString(type: Copy) {
  from '../images/java.jpg'
  into "${buildDir}/java-image"
}
task copyByFile(type: Copy) {
  from file('../images/java.jpg')
  into file('build/java-image')
}
----

====

.`buildDir` とは？
[TIP]
====

プロジェクトの成果物を配置するディレクトリーです。ほぼすべてのタスクの成果物はこのディレクトリーに配置されます。
デフォルトのディレクトリーのパスは `projectDir/build` となっています。

なお、 `clean` タスクは `project.buildDir` ディレクトリーを削除するタスクです。

====

== ディレクトリーごとコピー

コピーはファイル単位だけでなく、ディレクトリー単位でも可能です。
その際は `from` にディレクトリーを指定します。

.ディレクトリーごとコピー
[source,groovy]
----
task copyDir(type: Copy) {
  from '../images/server'
  into "${buildDir}/copy-dir"
}
----

また、特に違いはありませんが、 `project.fileTree` メソッドによって、 `ConfigurableFileTree` オブジェクトを構築して `from` に指定することも可能です。動作としては、 `from` にディレクトリーを指定したものと同様の動きに鳴ります。

.`fileTree` を用いてディレクトリーごとコピー
[source,groovy]
----
task copyTree(type: Copy) {
  from fileTree('../images/server')
  into "${buildDir}/copy-tree"
}
----

.`ConfigurableFileTree` のメリットは何？
[TIP]
====

`ConfigurableFileTree` は指定したベースディレクトリーからのツリー構造をたどって得られるファイルのリストのようなものです。

`List` との違いは `List` は内容がすぐに評価されるのに対して、 `ConfigurableFileTree` では必要になった時点でリストとして評価することができる点です。

例えばまだツリーの内部にファイルが存在していない場合は、リストではディレクトリーの下にあるファイルを列挙することはできませんが、 `ConfigurableFileTree` では必要になった時に始めてディレクトリーの内部を走査するので、設定時にはなかったファイルを取り扱うことができます。

====

=== 演習3

`copy-projects/images` の下にあるファイルをディレクトリー構造を維持したまま `build/images` ディレクトリーにコピーする `copyImages` タスクを作成してください。

== ファイルのフィルタリング

ファイルをコピーする際に特定の条件を指定したい場合などがあると思います。例えば、 `課題管理票.xlsx` はコピーするけど、 `課題管理票_bk.xlsx` はコピーしないなど。

このようなコピーするファイルのフィルタリング機能を `Copy` タスクは提供してくれています。

.pngファイルだけをコピーする
[source,groovy]
----
task copyOnlyPng(type: Copy) {
  from '../images/tool'
  into "${buildDir}/only-png"
  include '*.png'
}
----

`include` を用いると指定されたパターンに合致するファイルだけがコピーの対象になります。

.png以外のファイルをコピーする
[source,groovy]
----
task copyNotPng(type: Copy) {
  from '../images/tool'
  into "${buildDir}/only-png"
  exclude '*.png'
}
----

`exclude` を用いると指定されたパターンに合致するファイルはコピーされません。

.パターン文字列
[TIP]
====

`include` および `exclude` はAntのパターン文字列で指定します。

.Antのパターン文字列
* `*` すべての文字列を含むファイル
** `*.class` -> 拡張子が `class` のファイルすべて
** `*Test$*` -> ファイル名の途中に `Test$` を含むファイルすべて
* `**` すべての文字列を含むディレクトリー
** `**tmp` -> `tmp` で終わるディレクトリーすべて
** `tmp**` -> `tmp` で始まるディレクトリー
** `**/img/*` -> すべてのディレクトリーの下にある `img` ディレクトリーの下にあるファイルすべて

.指定例
* `com/fasterxml/` の下にあるディレクトリーファイルすべて
** `**/com/fasterxml/**/*`
* 対象のディレクトリーの中からファイル名に `Test$` が含まれるファイルすべて
** `**/*Test$*`

====

=== 演習4

.上記の `copyOnlyPng` と `copyNotPng` タスクを実行してください。
* `copyOnlyPng` がpngファイルだけをコピーしていることを確認して下さい。
* `copyNotPng` がpngファイル以外をコピーしていることを確認して下さい。

== より複雑なフィルタリング

先ほどの例では `Copy` タスクの `include`/`exclude` を用いましたが、 `from` メソッドでフィルタリングを行うことも可能です。

.`from` メソッドの `include`/`exclude`
[source,groovy]
----
task copyComplex(type: Copy) {
  from ('../images/jvm') { /* <1> */
    exclude 'g*'
  }
  from ('../images/tool') { /* <2> */
    include 'g*'
  }
}
----
<1> `images/jvm` ディレクトリーからは `g` で始まるファイル以外をコピーする
<2> `images/tool` ディレクトリーからは `g` で始まるファイルのみをコピーする

=== 演習5

.上記の `copyComplex` タスクを実行してください。
* `images/jvm` ディレクトリーから `scala.png` ファイルがコピーされているか確認して下さい。
* `images/tool` ディレクトリーから `gradle.gif` ファイルがコピーされているか確認して下さい。

=== 演習6

.次の仕様を満たすコピータスクを作ってください。
* `images` ディレクトリー全体からpngファイルだけをコピーする
** ただし、ファイル名に `oo` を含むファイルは除外する
* また `j` で始まるものは拡張子が何でもコピーする
* コピー先は `build/complex` ディレクトリーとする
