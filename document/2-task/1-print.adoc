= Taskの作り方

タスクは `Project` オブジェクトの `task` というメソッドによって作ることができます。(正確に言うと `TaskContainer` というオブジェクトに委譲されている `task` メソッドによって作る)

[source, groovy]
----
task my1stTask /* <1> */
task my2ndTask { doSomeConfiguration() } /* <2> */
task my3rdTask << { doSomeAction() } /* <3> */
task my4thTask (type: SomeTypeTask) /* <4> */
task my5thTask (type: SomeTypeTask) { doSomeConfiguration() } /* <5> */
----
<1> `my1stTask` 何もしないタスクを作ります。
<2> `my2ndTask` 何らかの設定が施されるけど、何もしないタスクを作ります。
<3> `my3rdTask` `doSomeAction` に記述されている処理をおこなうタスクを作ります。
<4> `my4thTask` `SomeTypeTask` 型のタスクを作成します。
<5> `my5thTask` `SomeTypeTask` 型のタスクを生成して、何らかの設定を行います。

== 演習

.それぞれのタスクを実行してみましょう。
. `task-projects/print-message` ディレクトリーに移動してください。
. `build.gradle` ファイルを読んでみましょう。
. `tasks` タスクを実行して、 `my1stTask` 〜 `my5thTask` があることを確認して下さい。
. `my1stTask` 〜 `my5thTask` をそれぞれ実行してみて下さい。
** スクリプトとから想像した動作と、実際の動作を比較して、何かおかしなことが発生していませんか？

.Groovyによる簡易な記法
[TIP]
====

Groovyではメソッドの多くが簡単に書けるようになっています。

'''

例えば標準出力に文字列を流すメソッド `System.out.println` はGroovyでは `println` に簡略化されています。

[source,java]
----
System.out.println("This is " + name + ".");
----

[source,groovy]
----
println "This is ${name}."
----

'''

また、setter/getterの呼び出しも簡略化されています。

[source,java]
----
myTask.setMessage("hello");
System.out.println(myTask.getMessage());
----

[source,groovy]
----
myTask.message = 'hello' /* -> setter呼び出し */
println myTask.message   /* -> getter呼び出し */
----

なお、Javaのsetter/getterは明示的にあるいはLombokなどの黒魔術によって生やす必要がありますが、Groovyではアクセスレベルをパッケージプライベート以上にすると勝手に生えてきます。

[source,java]
----
public class MyTask extends DefaultTask {
  String message;
  public String getMessage() {
    return message;
  }
  @TaskAction
  public void work() {}
}
----

[source,groovy]
----
class MyTask extends DefaultTask {
  String message /* -> getter/setterが自動で生成される */
  @TaskAction void work() {}
}
----

====

= Configuration と Action

`my2ndTask` と `my3rdTask` の挙動に違和感を抱かれた方が多いのではないでしょうか。

.同じ標準出力に流す `println` メソッドを利用しているにもかかわらず...
* `my2ndTask` の出力内容はすべてのタスクで表示される。
* `my3rdTask` の出力内容は `my3rdTask` 実行時のみ表示される。

== Gradleのビルドフェーズ

.Gradleのビルドは次の順序で実行されます。
. Initialization (初期化フェーズ)
** プロパティの読み込み
** 設定(`settings.gradle`)の読み込み
** 関連するすべての `Project` オブジェクトを構築(サブプロジェクトなどの情報をあつめて構築)
. Configuration (設定フェーズ)
** 構築された `Project` オブジェクトに対して `build.gradle` に記述された設定を適用していく
** タスクの設定を施す
** 依存ライブラリーやタスクの依存関係の情報を収集・構築
. Execution (実行フェーズ)
** 指定されたタスクを抽出して、Task Actionを実行する

== Configuration と Action 再び

再び `my2ndTask` と `my3rdTask` の定義を見てみましょう。

.`my2ndTask`
****

[source,groovy]
----
task my2ndTask {
  println "something"
  group = 'my task'
  /* and so on */
}
----

****

.`my3rdTask`
****

[source,groovy]
----
task my3rdTask << {
  println 'something'
}
----

****

違いは `my3rdTask` の方ではブロック `{}` の前に演算子 `<<` が付いていることです。

演算子 `<<` はGroovyでは `leftShift` というメソッドで定義されるメソッドで、これの具体的な実装は内部的に `doLast` メソッドを呼び出しているだけです。

そして、 `doLast` メソッドの定義はこのようなものです

[quote, 'https://docs.gradle.org/current/dsl/org.gradle.api.Task.html#org.gradle.api.Task:doLast(groovy.lang.Closure)[Task - Gradle DSL]']
----
Adds the given closure to the end of this task's action list. <1>
----
<1> (意訳)タスクの処理リストの最後に与えられた新しいアクションを追加する。

したがって、ここで使われている `Closure`(`{}`)はTask Actionとなります。

'''

一方の `my2ndTask` の記述は上の方でちょこっと書いた、 `TaskContainer` の `create(String name, Closure configureClosure)` を呼び出す記法です。

引数の名前から想像できる通り、ここで使われている `Closure`(`{}`)は Configuration フェーズで実行される `Closure` となります。

=== Configuration と Action 結論

`Closure` の位置について注意を払ってTaskの定義を記述するようにしましょう。

.`Closure` の位置と実行されるフェーズ
* Configuration
** `task name {/* configuration(configuration phase) */}`
* Task Action
** `task name << {/* task action(execution phase) */}`

.`Closure` って何？
[TIP]
====

先ほどから `Closure` というクラス(オブジェクト)が出てきていますが、これは何でしょう？

簡単に言うとJavaの `java.util.function.Function<T, R>` みたいなものです。

記述の仕方によっては `java.util.function.BiFunction<T, U, R>` のようにも、 `java.util.function.Predicate<T>` のようにも、 `java.lang.Runnable` のようにも、 `java.util.concurrent.Callable<V>` のようにも使えます。

Groovy2.2からはJava 8の `Stream` の各種中間操作などに渡される `Function`、 `Predicate` などに `Closure` を渡すことができるようになっています。

[source, groovy]
----
def list = Stream.of('ant', 'maven', 'sbt', 'gradle')
        .filter {it.size() > 3}
        .map {it.toUpperCase()}
        .collect(Collectors.toList())

assert list == ['MAVEN', 'GRADLE']
----

また、 `Closure` の中では渡された引数は `it` で参照することが可能です。

====

= 演習1

* `task-projects/print-message` プロジェクトの `build.gradle` にConfiguration Phaseに現在時刻を表示するタスク `timeOfConf` とタスク実行時の時刻を表示する `timeOfExec` タスクを作ってみましょう。
** 表示する時刻は `yyyy-MM-dd HH:mm:ss.SSS` の形式で表示してください。
*** Configurationフェーズに処理を実行させるにはどう記述すればよいのでしょうか？
*** Executionフェーズ(Task Action)に処理を実行させるにはどのように記述すればよいのでしょうか？

.Groovyにおける時刻のフォーマット
[TIP]
====

Groovyでは `Date` クラスが拡張されて、便利なメソッドが追加されています。

[source,groovy]
----
assert new Date(0).format('yyyy-MM-dd HH:mm:ss.SSS') == '1970-01-01 09:00:00.000'
----

====

= Configurationで設定できる基本的な項目

すべてのタスクに共通して、Configuration Closureで設定できる項目には次のようなものがあります。

.Taskに共通する設定項目
|===
|プロパティ |型 |内容

|`group`
|`String`
|タスクが所属するグループ名

|`description`
|`String`
|タスクの説明

|`type`
|`Class<? extends Task>`
|タスクのひな形

|`dependsOn`
|`Object...`
|タスクが依存するタスク(例：jarに固める前にjavaのコンパイルが必要など)
|===

= 演習2

. `my1stTask` 〜 `my5thTask` が `my2ndTask` と同じ `my task` グループに所属するように設定を記述してください。
. 演習1で作成した `timeOfExec` タスクが `my3rdTask` の後に実行されるように設定してください。

.Configurationの記述方法
[TIP]
====

Configurationを記述する方法は3通りあります。

. `Map<String, Object>` で設定する方法
. `Closure` で設定する方法
. プロパティ(setter)で設定する方法

.`Map<String, Object>` で設定する方法
****

[source,groovy]
----
task myTask(description: 'Description for myTask.')
----

****

.`Closure` で設定する方法
****

[source,groovy]
----
task myTask {
  description = 'Description for myTask.'
}
----

****

.プロパティ(setter)で設定する方法
****

[source,groovy]
----
task myTask

myTask.description = 'Description for myTask.'
----

****

====

